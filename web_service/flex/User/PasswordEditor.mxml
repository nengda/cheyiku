<?xml version="1.0" encoding="utf-8"?>
<DialogFixed xmlns="DTS.UI.*" xmlns:mx="http://www.adobe.com/2006/mxml" width="360" height="170" title="用戶密码" layout="absolute" borderThicknessLeft="0" borderThicknessRight="0" creationComplete="OnCreationComplete()">
	<mx:Script>
		<![CDATA[
			import DTS.Net.HTTPClient;
			import DTS.UI.BlockingWindow;
			import DTS.UI.MessageBox;
			import DTS.Logger.LogWindow;

			private var _user:XML = null;

			public static function Open(user:XML):PasswordEditor {
				var dlg:PasswordEditor = DialogFixed.Open(PasswordEditor) as PasswordEditor;
				dlg._user = user;
				return dlg;
			}

			private function OnCreationComplete():void {
				if (_user == null) {
					callLater(OnCreationComplete);
					return;
				}
				txtPassword.setFocus();
			}

			private function Save():void {
				if (txtPassword.text.match(/^\s*$/) != null) {
					MessageBox.Show("新密码呢？\n\n", "", function():void{
						txtPassword.setFocus();
					});
					return;
				}
				if (txtRepeat.text.match(/^\s*$/) != null) {
					MessageBox.Show("重复一下？\n\n", "", function():void{
						txtRepeat.setFocus();
					});
					return;
				}
				if (txtRepeat.text != txtPassword.text) {
					MessageBox.Show("密码重复得不一样？\n\n", "", function():void{
						txtPassword.setFocus();
					});
					return;
				}

				visible = false;
				var blockingWindow:BlockingWindow = BlockingWindow.Open();
				var request:Object = {
					"email": _user.email,
					"password": txtPassword.text
				};
				HTTPClient.Post(
					"PasswordEditor",
					Settings.EndPoint + "update_user_password.svc",
					request,
					function(response:String):void {
						blockingWindow.Close();
						visible = true;
						var xml:XML = XML(response);
						if (xml.status.toString().toLowerCase() == "ok") {
							GlobalEvents.FireEvent(GlobalEvents.REFRESH_USERS);
							Close();
						}
						else {
							MessageBox.Show(xml.data.toString() + "\n\n");
						}
					}
				);
			}
		]]>
	</mx:Script>
	<mx:Canvas borderStyle="solid" borderSides="top,bottom" left="0" right="0" top="0" bottom="0" backgroundColor="#FFFFFF">
		<mx:Label x="20" y="22" text="新密码:"/>
		<mx:TextInput id="txtPassword" y="20" left="99" right="25" displayAsPassword="true"/>
		<mx:Label x="20" y="50" text="重复:"/>
		<mx:TextInput id="txtRepeat" y="48" left="99" right="25" displayAsPassword="true"/>
	</mx:Canvas>
	<ControlBarResizable horizontalAlign="right" x="0" y="276" paddingTop="8">
		<mx:Button label="保存" id="btnOK" click="Save()"/>
		<mx:Button label="取消" id="btnCancel" click="Close()"/>
	</ControlBarResizable>
</DialogFixed>
