<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns="*" xmlns:mx="http://www.adobe.com/2006/mxml" label="客户" creationComplete="OnCreationComplete()">
	<mx:Script>
		<![CDATA[
			import DTS.Core.GenericEvent;
			import DTS.Net.HTTPClient;
			import DTS.UI.DataGridClasses.FilterableHeader;
			import DTS.UI.DataGridClasses.FilterPanels.FilterPanelBase;
			import DTS.UI.DataGridClasses.FilterPanels.FilterPanelString;
			import DTS.UI.DataGridClasses.FilterPanels.FilterPanelEnum;
			import DTS.UI.DataGridClasses.FilterPanels.FilterPanelDate;
			import DTS.Utilities.DataGridColumns;
			import DTS.Utilities.ListDoubleClick;
			import DTS.UI.Colors;
			import mx.collections.XMLListCollection;
			import mx.events.ListEvent;
			import mx.skins.ProgrammaticSkin;

			private static var _instance:ClientPage = null;
			public static function get Instance():ClientPage {
				return _instance;
			}

			protected var _filterPanels:Array = null;

			private var _years:Array = null;
			public function get Year():Array {
				return _years;
			}

			private var _district:Array = null;
			public function get District():Array {
				return _district;
			}

			private var _profession:Array = null;
			public function get Profession():Array {
				return _profession;
			}

			private var _service:Array = null;
			public function get Service():Array {
				return _service;
			}

			private var _carBrand:Array = null;
			public function get CarBrand():Array {
				return _carBrand;
			}

			private var _carModel:Object = null;
			public function get CarModel():Object {
				return _carModel;
			}

			private function OnCreationComplete():void {
				_instance = this;
				InitFormData();
				GlobalEvents.AddEventHandler(GlobalEvents.REFRESH_CLIENTS, function(event:GenericEvent):void {
					Refresh();
				});
			}

			private function InitFormData():void {
				if (pnlLoading == null) {
					callLater(InitFormData);
					return;
				}

				if (pnlLoading.visible) {
					return;
				}

				pnlLoading.visible = true;
				HTTPClient.Post(
					"ClientPage",
					Settings.EndPoint + "list_form_data.svc",
					{},
					function(response:String):void {
						pnlLoading.visible = false;
						var xmlForm:XML = XML(response);
						_profession = RenderFormData(xmlForm.data.profession.toString());
						_district = RenderFormData(xmlForm.data.district.toString());
						_service = RenderFormData(xmlForm.data.service.toString());
						_carBrand = new Array();
						_carModel = new Object();
						for each (var car:XML in XML(xmlForm.data.car).children()) {
							_carBrand.push(car.localName());
							_carModel[car.localName()] = RenderFormData(car.toString());
						}
						_years = new Array();
						var thisYear:Number = new Date().fullYear;
						var year:Number = 1990;
						while (year <= thisYear) {
							_years.push(year++);
						}
						Refresh();
					},
					function():void {
						pnlLoading.visible = false;
					}
				);
			}
			
			private function RenderFormData(fs:String):Array {
				var fd:Array = fs.split(",");
				var fo:Array = new Array();
				for each (var fs:String in fd) {
					var so:Array = fs.split("|");
					fo.push({id: so[0], name: so[1]});
				}
				return fo;
			}


			public function Refresh():void {
				if (pnlLoading == null) {
					callLater(Refresh);
					return;
				}

				if (pnlLoading.visible) {
					return;
				}

				pnlLoading.visible = true;
				HTTPClient.Post(
					"ClientPage",
					Settings.EndPoint + "list_clients.svc",
					{},
					function(response:String):void {
						pnlLoading.visible = false;
						var xmlPro:XML = XML(response);
						var clients:XMLList = xmlPro.data.client;
						var dp:XMLListCollection = new XMLListCollection(clients);
						dp.filterFunction = FilterFunction;
						dp.refresh();
						dgClients.dataProvider = dp;
					},
					function():void {
						pnlLoading.visible = false;
					}
				);
			}

			private function FilterFunction(o:Object):Boolean {
				for each (var fp:FilterPanelBase in _filterPanels) {
					if (!fp.Filter(o)) {
						return false;
					}
				}
				return true;
			}

			private function OnFilterChanged(event:Event=null):void {
				callLater(function():void {
					var dp:XMLListCollection = dgClients.dataProvider as XMLListCollection;
					if (dp != null) {
						dp.refresh();
						dgClients.invalidateList();
					}
				});
			}

			private function InitDataGrid():void {
				var columns:Array = new Array();
				_filterPanels = new Array();
				AddColumn(columns, _filterPanels,
					{headerText: "辨识码", width: 80, dataField: "id"},
					new FilterPanelString("id", OnFilterChanged)
				);
				AddColumn(columns, _filterPanels,
					{headerText: "推广人", width: 80, dataField: "promoter"},
					new FilterPanelString("promoter", OnFilterChanged)
				);
				AddColumn(columns, _filterPanels,
					{headerText: "姓名", width: 80, dataField: "full_name"},
					new FilterPanelString("full_name", OnFilterChanged)
				);
				AddColumn(columns, _filterPanels,
					{headerText: "品牌", width: 80, dataField: "car_brand"},
					new FilterPanelString("car_brand", OnFilterChanged)
				);
				AddColumn(columns, _filterPanels,
					{headerText: "车型", width: 80, dataField: "car_model"},
					new FilterPanelString("car_model", OnFilterChanged)
				);
				AddColumn(columns, _filterPanels,
					{headerText: "公里数", width: 80, dataField: "mileage"},
					new FilterPanelString("mileage", OnFilterChanged)
				);
				AddColumn(columns, _filterPanels,
					{headerText: "购买时间(年)", width: 80, dataField: "year_of_purchase"},
					new FilterPanelString("year_of_purchase", OnFilterChanged)
				);
				AddColumn(columns, _filterPanels,
					{headerText: "服务", dataField: "services"},
					new FilterPanelString("services", OnFilterChanged)
				);
				AddColumn(columns, _filterPanels,
					{headerText: "邮箱", dataField: "email"},
					new FilterPanelString("email", OnFilterChanged)
				);
				AddColumn(columns, _filterPanels,
					{headerText: "手机号码", dataField: "mobile_number"},
					new FilterPanelString("mobile_number", OnFilterChanged)
				);
				AddColumn(columns, _filterPanels,
					{headerText: "创建时间", dataField: "create_timestamp", datetime: true},
					new FilterPanelDate("create_timestamp", OnFilterChanged)
				);
				AddColumn(columns, _filterPanels,
					{headerText: "更新时间", dataField: "update_timestamp", datetime: true},
					new FilterPanelDate("update_timestamp", OnFilterChanged)
				);
				AddColumn(columns, _filterPanels,
					{headerText: "", dataField: "", width: 60, resizable: false, sortable: false, itemRenderer: {
						rendererClass: ClientActions, properties: { IsItemHighlighted: dgClients.isItemHighlighted }
					}}
				);
				DataGridColumns.Create(dgClients, columns);
				ListDoubleClick.OnItemDoubleClicked(dgClients, function(item:Object):void {
					var xml:XML = item as XML;
					switch (xml.localName().toString().toLowerCase()) {
						case "client":
							ClientEditor.Open(xml);
							break;
					}
				});
			}

			private function AddColumn(columns:Array, filterPanels:Array, c:Object, f:FilterPanelBase=null):void {
				if (f != null) {
					c["headerRenderer"] = {
						rendererClass: FilterableHeader,
						properties: { FilterPanel: f }
					};
					filterPanels.push(f);
				}
				columns.push(c);
			}

			private function OnItemRollOver(event:ListEvent):void {
				try {
					ClientActions.ItemToInstance(event.itemRenderer.data).alpha = 1;
				}
				catch (error:Error) {}
			}

			private function OnItemRollOut(event:ListEvent):void {
				try {
					ClientActions.ItemToInstance(event.itemRenderer.data).alpha = 0;
				}
				catch (error:Error) {}
			}
		]]>
	</mx:Script>
	<mx:DataGrid id="dgClients" left="0" right="0" top="0" bottom="36" borderSides="top" rowHeight="24" allowMultipleSelection="false" alternatingItemColors="[#FFFFFF, #FFFFFF]" horizontalGridLines="false" verticalGridLines="false" creationComplete="InitDataGrid()" itemRollOver="OnItemRollOver(event)" itemRollOut="OnItemRollOut(event)"/>
	<mx:Canvas id="pnlLoading" left="10" bottom="46" borderStyle="solid" borderThickness="0" backgroundColor="{Colors.THEME_COLOR}" cornerRadius="5" visible="false" backgroundAlpha="0.8">
		<mx:Label text="加载中..." color="#FFFFFF" fontWeight="bold" left="10" right="10" top="5" bottom="5"/>
	</mx:Canvas>
	<mx:Canvas height="36" borderSides="top" borderStyle="solid" bottom="0" left="0" right="0" backgroundColor="#EEEEEE">
		<mx:HBox verticalCenter="0" verticalAlign="middle" right="10" horizontalGap="0">
			<mx:Button id="btnAdd" label="新客户" icon="{Icons.ICO_ADD}" skin="mx.skins.ProgrammaticSkin">
				<mx:click>
					<![CDATA[
						ClientEditor.Open(XML(<client/>));
					]]>
				</mx:click>
				<mx:mouseOver>
					<![CDATA[
						btnAdd.setStyle("textDecoration", "underline");
					]]>
				</mx:mouseOver>
				<mx:mouseOut>
					<![CDATA[
						btnAdd.setStyle("textDecoration", "none");
					]]>
				</mx:mouseOut>
			</mx:Button>
			<mx:Button id="btnRefresh" label="刷新" icon="{Icons.ICO_REFRESH}" skin="mx.skins.ProgrammaticSkin" click="Refresh()">
				<mx:mouseOver>
					<![CDATA[
						btnRefresh.setStyle("textDecoration", "underline");
					]]>
				</mx:mouseOver>
				<mx:mouseOut>
					<![CDATA[
						btnRefresh.setStyle("textDecoration", "none");
					]]>
				</mx:mouseOut>
			</mx:Button>
		</mx:HBox>
	</mx:Canvas>
</mx:Canvas>
